cmake_minimum_required(VERSION 3.19)

set(Fernet_known_comps static shared)
set(Fernet_comp_static NO)
set(Fernet_comp_shared NO)
foreach (Fernet_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (Fernet_comp IN_LIST Fernet_known_comps)
        set(Fernet_comp_${Fernet_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "Fernet does not recognize component `${Fernet_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if (Fernet_comp_static AND Fernet_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
        "Fernet `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(Fernet_static_targets "${CMAKE_CURRENT_LIST_DIR}/Fernet-static-targets.cmake")
set(Fernet_shared_targets "${CMAKE_CURRENT_LIST_DIR}/Fernet-shared-targets.cmake")

macro(Fernet_load_targets type)
    if (NOT EXISTS "${Fernet_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "Fernet `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${Fernet_${type}_targets}")
endmacro()

if (Fernet_comp_static)
    Fernet_load_targets(static)
elseif (Fernet_comp_shared)
    Fernet_load_targets(shared)
elseif (DEFINED Fernet_SHARED_LIBS AND Fernet_SHARED_LIBS)
    Fernet_load_targets(shared)
elseif (DEFINED Fernet_SHARED_LIBS AND NOT Fernet_SHARED_LIBS)
    Fernet_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${Fernet_shared_targets}")
        Fernet_load_targets(shared)
    else ()
        Fernet_load_targets(static)
    endif ()
else ()
    if (EXISTS "${Fernet_static_targets}")
        Fernet_load_targets(static)
    else ()
        Fernet_load_targets(shared)
    endif ()
endif ()
